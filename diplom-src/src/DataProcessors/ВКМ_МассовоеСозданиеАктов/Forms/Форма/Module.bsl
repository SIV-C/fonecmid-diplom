
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	Объект.НовыеРеализации.Очистить();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьАкты(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Период) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Период не выбран");
		Возврат;
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = Новый ОписаниеОповещения("ОповещениеОПрогрессе", ЭтотОбъект);
	ПараметрыОжидания.Интервал = 1;
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;  
		
	ДлительнаяОперация = СоздатьАктыНаСервере();
	Оповещение = Новый ОписаниеОповещения("ПриЗавершении", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СоздатьАктыНаСервере()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "Обработка.ЗагрузкаДанных.МодульОбъекта.СоздатьАкты",
		Объект.Период);

КонецФункции

&НаКлиенте
Процедура ПриЗавершении(Результат, ДопПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	Если Результат.Статус = "Выполнено" Тогда
		АдресРезультата = Результат.АдресРезультата;
		Реализации = ПолучитьИзВременногоХранилища(АдресРезультата);
		ЗаполнитьАкты(Реализации);		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьАкты(Реализации)
	
	Объект.ДокументыЗаПериод.Очистить();
	
	Для Каждого СписокРеализацияСтруктура Из Реализации Цикл
		
		НоваяСтрока = Объект.ДокументыЗаПериод.Добавить();
		НоваяСтрока.Договор = СписокРеализацияСтруктура.Договор;
		НоваяСтрока.ДокументРеализация = СписокРеализацияСтруктура.Реализация;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОПрогрессе(Прогресс, ДополнительныеПараметры) Экспорт

    Если Прогресс.Прогресс <> Неопределено Тогда
        Состояние("выполняется заполнение докуметов", Прогресс.Прогресс.Процент);
    КонецЕсли;
	
КонецПроцедуры 

#КонецОбласти
